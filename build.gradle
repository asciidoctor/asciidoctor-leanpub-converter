// ============================================================================
/*
 * Copyright 2013-2014 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
// ============================================================================

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctorj-diagram:1.5.4'
    }
}

plugins {
    id 'com.jfrog.artifactory' version '4.4.17' apply false
    id 'com.jfrog.bintray' version '1.7' apply false
    id 'org.ysb33r.cloudci' version '1.0'
}

allprojects {
    group = 'org.asciidoctor'
    version = '1.6.0-alpha.2-SNAPSHOT'

    ext {
        asciidoctorjVersion = '1.6.0-alpha.4'
    }

}

subprojects {

    repositories {
        jcenter()
        if (version.endsWith('-SNAPSHOT')) {
            maven {
                name 'Asciidoctor Snapshots'
                url 'https://oss.jfrog.org/artifactory/oss-snapshot-local'
            }
        }
    }
}

task leanpubBook ( type : GradleBuild ) {

    dependsOn ':asciidoctor-leanpub-markdown:jar', ':asciidoctor-leanpub-markdown:test'

    tasks = ['asciidoctor']
    dir = file("${projectDir}/leanpub-book")
    buildFile = file("${projectDir}/leanpub-book/build.gradle")
    startParameter.projectProperties.version = version
    startParameter.projectProperties.asciidoctorjVersion = asciidoctorjVersion
    startParameter.projectProperties.jarLocation = new File(project(':asciidoctor-leanpub-markdown').buildDir,'libs').absolutePath
}